import groovy.json.JsonBuilder
import groovy.json.JsonOutput
import nz.govt.natlib.tools.sip.generation.parameters.Spreadsheet
import nz.govt.natlib.tools.sip.generation.parameters.SpreadsheetImporter
import nz.govt.natlib.m11n.tools.gradle.BuildException
import nz.govt.natlib.m11n.tools.gradle.ProjectHelper

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'nz.govt.natlib.m11n.tools:automation-plugin:1.0.0-SNAPSHOT'
        classpath 'nz.govt.natlib.m11n.tools:gradle-plugin:1.0.0-SNAPSHOT'
        classpath 'nz.govt.natlib.tools:sip-generation-gradle-plugin:1.0.0-SNAPSHOT'
    }
}

plugins {
    // required for jacoco test coverage checks
    id 'com.github.kt3k.coveralls' version '2.6.3'
}

project.ext {
    // TODO Will need tasks associated with jacoco to actually use these limits
    // TODO Generally the goal here is to have code coverage in the 80--90% range
    // but it may be better to approach this with a more directed focus
    // (i.e. increase code coverage when new functionality is added)
    // TODO Write unit tests!
    codeCoverageLimits = [
            'instruction': 0.0, // currently at 0.0%
            'branch'     : 0.0, // currently at 0.0%
            'line'       : 0.0, // currently at 0.0%
            'complexity' : 0.0, // currently at 0.0%
            'method'     : 0.0, // currently at 0.0%
            'class'      : 0.0  // currently at 0.0%
    ]

    // Command-line variables used for specific tasks
    // Task updateDefaultFairfaxImportParameters
    doUpdateDefaultFairfaxImportParameters = ProjectHelper.hasAnyTasks(project, [ "updateDefaultFairfaxImportParameters" ])
    fairfaxSpreadsheetImportFilenameValue = project.hasProperty('fairfaxSpreadsheetImportFilename') ?
            project.fairfaxSpreadsheetImportFilename : null
    fairfaxSpreadsheetExportFilenameValue = project.hasProperty('fairfaxSpreadsheetExportFilename') ?
            project.fairfaxSpreadsheetExportFilename : "core/src/main/resources/nz/govt/natlib/tools/sip/generation/fairfax/default-fairfax-import-parameters.json"
}

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'jacoco'

// run the findBugs checks with 'gradle check' or 'gradle findBugsMain' or 'gradle findBugsTest'
//apply plugin: 'findbugs'

// run the pmd source code analyzer with 'gradle check' or 'gradle pmdMain' or 'gradle pmdTest'
apply plugin: 'pmd' // source code analyzer

setVersion(project.ext.versionNumber)

dependencies {
    // localGroovy (which is currently 2.4.x) is used. Groovy 2.5.x supports JsonGenerator and other new functionality
    // but causes issues with downstream builds, so alternatives provided by Groovy 2.4.x are used.
    compile localGroovy()
    //compile 'org.codehaus.groovy:groovy-all:2.5.4'

    // Use the latest version
    compile "com.google.guava:guava:+"
    // Use the latest version
    compile "org.apache.commons:commons-lang3:+"
    compile "commons-io:commons-io:2.+"

    compileOnly 'org.slf4j:slf4j-api:1.7.+'

    compile 'nz.govt.natlib.tools:sip-generation-core:1.0.0-SNAPSHOT'
    // latest version
    testCompile 'junit:junit:4.12'

    // latest version
    testCompile 'org.mockito:mockito-all:1.10.19'

    // use sl4j logging for tests
    testCompile 'org.slf4j:slf4j-log4j12:1.7.+'
}

jar {
    includeEmptyDirs = false
}

task jarSources(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    includeEmptyDirs = false

    from sourceSets.main.allSource
}

task updateDefaultFairfaxImportParameters() {
    File spreadsheetImportFile
    if (project.doUpdateDefaultFairfaxImportParameters) {
        if (project.fairfaxSpreadsheetImportFilenameValue == null) {
            throw new BuildException("fairfaxSpreadsheetImportFilename=${project.fairfaxSpreadsheetImportFilenameValue} must be set when calling task 'updateDefaultFairfaxImportParameters'.")
        } else {
            spreadsheetImportFile = new File((String) project.fairfaxSpreadsheetImportFilenameValue)
        }
        if (!spreadsheetImportFile.exists()) {
            throw new BuildException("File=${spreadsheetImportFile.getCanonicalPath()} must exist.")
        }
        if (!spreadsheetImportFile.isFile()) {
            throw new BuildException("File=${spreadsheetImportFile.getCanonicalPath()} must be a file.")
        }
    }
    doLast {
        File spreadsheetExportFile = new File(project.fairfaxSpreadsheetExportFilenameValue)
        logger.info("Writing spreadsheet-import-file=${spreadsheetImportFile.getCanonicalPath()} to spreadsheet-export-file=${spreadsheetExportFile.getCanonicalPath()}")
        boolean allowDuplicateIds = true
        boolean allowRowsWithoutIds = true
        // TODO Make the separator configurable
        Spreadsheet spreadsheet = SpreadsheetImporter.extractSpreadsheet(spreadsheetImportFile, "MMSID", "|", allowDuplicateIds, allowRowsWithoutIds)
        logger.info("spreadsheet values imported:")
        spreadsheet.rows.each { Map<String, String> spreadsheetLine ->
            StringBuilder line = new StringBuilder()
            spreadsheetLine.each { String key, String value ->
                line.append("${key}:${value}|")
            }
            logger.info(line.toString())
        }
        JsonBuilder jsonBuilder = new JsonBuilder()

        spreadsheetExportFile.write(JsonOutput.prettyPrint(spreadsheet.asJsonString()))
    }
}

//tasks.withType(FindBugs) {
//    // Unfortunately FindBugs will find a lot of problems with Groovy classes. We could filter out groovy source files,
//    // but since FindBugs operates on .class files, that filtering will be ineffective. Currently there are no java
//    // classes but that could change in the future.
//    // See https://stackoverflow.com/questions/22037499/using-excludes-config-in-findbugs-and-checkstyle-plugin-in-gradle
//    // In order for Java classes to see Groovy classes easily, we compile them all together with the Groovy compiler
//    // in the same source tree (src/main/groovy). This means we can't use the path to determine if the .class file
//    // came from a Groovy or Java source. The net effect is that FindBugs won't work with a mixed heritage of
//    // Java and Groovy classes. So we remove the FindBugs plugin for now.
//    exclude '**/*.groovy'
//    classes = classes.filter {
//        !(it.path.contains(new File("classes/groovy/main").path) || it.path.contains(new File("classes/groovy/test").path))
//    }
//}

// We want to force a specific version as part of our dependency resolution
//configurations.all {
//    resolutionStrategy {
//        force 'org.codehaus.groovy:groovy-all:2.4.+'
//    }
//}

artifacts {
    publishableArtifacts jar, jarSources
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact tasks.jarSources
        }
    }

    repositories {
        maven {
            mavenLocal()
        }
    }
}

build.dependsOn jar, jarSources
